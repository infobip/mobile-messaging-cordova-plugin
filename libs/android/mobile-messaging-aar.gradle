def overrideAndroidSupportLibsVersion = cdvHelpers.getConfigPreference("IB_ANDROID_SUPPORT_VER_OVERRIDE", "false")
def overrideGmsVersion = cdvHelpers.getConfigPreference("IB_ANDROID_GMS_VER_OVERRIDE", "false")
def overrideFirebaseVersion = cdvHelpers.getConfigPreference("IB_ANDROID_FIREBASE_VER_OVERRIDE", "false")
def withCryptorMigration = cdvHelpers.getConfigPreference("IB_ANDROID_USE_CRYPTOR_MIGRATION", "false")

if (overrideAndroidSupportLibsVersion == "false") overrideAndroidSupportLibsVersion = ""
if (overrideGmsVersion == "false") overrideGmsVersion = ""
if (overrideFirebaseVersion == "false") overrideFirebaseVersion = ""

if (overrideAndroidSupportLibsVersion.empty || overrideAndroidSupportLibsVersion > "26") {
    repositories {
        mavenLocal()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven { url 'https://developer.huawei.com/repo/' }
        mavenCentral()
    }
}

buildscript {
    repositories {
        maven { url "https://maven.google.com" }
        maven { url 'https://developer.huawei.com/repo/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

// apply plugin: 'com.huawei.agconnect'

dependencies {
    def mmHmsVersion = '4.0.2'
    def mmFcmVersion = '8.4.0'
    // Modified by scripts/hms_fix_dependencies.js . To enable run: cordova build android --hms
    def isHmsBuild = false

    // Android dependencies start
    if (!isHmsBuild) {
        // geofencing is disabled for rc version, will be enabled in next releases
//        implementation "com.infobip:infobip-mobile-messaging-android-geo-sdk:$mmFcmVersion@aar"
        implementation "com.infobip:infobip-mobile-messaging-android-chat-sdk:$mmFcmVersion@aar"
        implementation("com.infobip:infobip-mobile-messaging-android-sdk:$mmFcmVersion@aar") {
            transitive = true;
            if (!overrideAndroidSupportLibsVersion.empty) {
                exclude group: 'androidx.legacy', module: 'legacy-support-v4'
                exclude group: 'androidx.appcompat', module: 'appcompat'
            }
            if (!overrideGmsVersion.empty) {
                exclude group: 'com.google.android.gms', module: 'play-services-location'
            }
            if (!overrideFirebaseVersion.empty) {
                exclude group: 'com.google.firebase', module: 'firebase-messaging'
            }
        }
    }
    // Android dependencies end

    // Hms dependencies start
    if (isHmsBuild) {
        // geofencing is disabled for rc version, will be enabled in next releases
//        implementation "com.infobip:infobip-mobile-messaging-huawei-geo-sdk:$mmHmsVersion@aar"
        implementation "com.infobip:infobip-mobile-messaging-huawei-chat-sdk:$mmHmsVersion@aar"
        implementation("com.infobip:infobip-mobile-messaging-huawei-sdk:$mmHmsVersion@aar") {
            transitive = true;
            if (!overrideAndroidSupportLibsVersion.empty) {
                exclude group: 'androidx.legacy', module: 'legacy-support-v4'
                exclude group: 'androidx.appcompat', module: 'appcompat'
            }
        }
    }
    // Hms dependencies end

    if (!overrideAndroidSupportLibsVersion.empty) {
        implementation "androidx.legacy:legacy-support-v4:$overrideAndroidSupportLibsVersion"
        implementation "androidx.appcompat:appcompat:$overrideAndroidSupportLibsVersion"
    }
    if (!overrideGmsVersion.empty) {
        implementation "com.google.android.gms:play-services-location:$overrideGmsVersion"
    }
    if (!overrideFirebaseVersion.empty) {
        implementation "com.google.firebase:firebase-messaging:$overrideFirebaseVersion"
    }
    if (withCryptorMigration.toBoolean()) {
        if (isHmsBuild) {
            implementation "com.infobip:infobip-mobile-messaging-huawei-cryptor-migration:$mmHmsVersion@aar"
        } else {
            implementation "com.infobip:infobip-mobile-messaging-android-cryptor-migration:$mmFcmVersion@aar"
        }
    }
}

